# Peekaping Service Module
#
# This module creates a Hetzner VM for Peekaping monitoring
# service using WebKit's server/volume modules.

terraform {
  required_providers {
    hcloud = {
      source  = "hetznercloud/hcloud"
      version = "~> 1.0"
    }
    local = {
      source  = "hashicorp/local"
      version = "~> 2.0"
    }
  }
}

provider "hcloud" {
  token = var.hetzner_token
}

# Hetzner Server (WebKit)
#
# This creates the VM, SSH keys, firewall, and installs Ansible via cloud-init.
module "server" {
  source = "github.com/ainsleydev/webkit//platform/terraform/providers/hetzner/server?ref=main"

  name        = var.service_name
  server_type = var.server_type
  location    = var.location
  tags        = var.tags
  ssh_key_ids = ["hello@ainsley.dev", "ainsley.clark@GGYN90GJW7"]
}

# Hetzner Volume (Webkit)
#
# Uses persistent data to save the Peekaping SQLite database.
# Note: lifecycle blocks cannot be used on modules, only resources.
# Data protection is provided by state backups and approval gates in CI/CD.
module "volume" {
  source = "github.com/ainsleydev/webkit//platform/terraform/providers/hetzner/volume?ref=main"

  name      = "${var.service_name}-data"
  size      = var.volume_size
  location  = var.location
  server_id = module.server.id
  format    = "ext4"
  automount = true
  tags      = concat(var.tags, [var.environment])
}

# Ansible Config
#
# Generate Ansible inventory file automatically
resource "local_file" "ansible_inventory" {
  filename = "${path.root}/../../ansible/inventory-peekaping.ini"
  content  = <<-EOT
    # Auto-generated by Terraform - DO NOT EDIT MANUALLY

    [peekaping]
    ${var.domain} ansible_host=${module.server.ip_address} ansible_user=${module.server.server_user}

    [peekaping:vars]
    domain=${var.domain}
    admin_email=${var.admin_email}
  EOT

  file_permission = "0644"
}
